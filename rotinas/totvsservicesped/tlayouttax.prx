#include "protheus.ch"
#include "parmtype.ch"

CLASS TLayOutTAX FROM LongClassName

	DATA cDescricao     AS CHARACTER
	DATA cDestino       AS CHARACTER
	DATA cObjetivo      AS CHARACTER
	DATA cPrazo	        AS CHARACTER
	DATA cAplicativo	AS CHARACTER
	DATA cComentario    AS CHARACTER
		
	DATA cLayOut        AS CHARACTER
	DATA cSaveFile      AS CHARACTER
	DATA cTempFile      AS CHARACTER
	DATA cMsgOutPutFile AS CHARACTER
	DATA cClassName     AS CHARACTER HIDDEN
	                            
	DATA aMsgStatus     AS ARRAY HIDDEN
	DATA aTables        AS ARRAY HIDDEN
	DATA aInit          AS ARRAY HIDDEN
	DATA aClose         AS ARRAY HIDDEN
	DATA aFormul        AS ARRAY
	
	DATA nHandle        AS NUMERIC HIDDEN
	
	DATA oLayOutTax     AS OBJECT HIDDEN
	
	METHOD New()       
	METHOD Init()      
	METHOD ReadLayOut()
	METHOD SaveLayOut()
	METHOD Execute()
	METHOD AddTables()
	METHOD AddOnInit()
	METHOD AddOnClose()
	METHOD AddFormul()
	METHOD ClassName()
	METHOD FindID()
	
ENDCLASS

CLASS TLayOutTAXTable FROM TLayOutTAX

	DATA cID          AS CHARACTER HIDDEN
	DATA cDescription AS CHARACTER HIDDEN
	DATA cAlias       AS CHARACTER READONLY
	
	DATA aWhile       AS ARRAY HIDDEN
	DATA aEnd         AS ARRAY HIDDEN
	DATA aCount       AS ARRAY HIDDEN
	DATA aRecord      AS ARRAY HIDDEN
	
	DATA oParent      AS OBJECT READONLY

	METHOD Init()		            
	METHOD New()
	METHOD AddRecord()
	METHOD AddOnInit()
	METHOD AddOnWhile()
	METHOD AddOnEnd()
	METHOD AddOnClose()
	METHOD Execute()
	
ENDCLASS

CLASS TLayOutTAXRecord FROM TLayOutTAXTable

	DATA aValid           AS ARRAY HIDDEN
	DATA aEstrutura       AS ARRAY HIDDEN
	DATA aConteudo        AS ARRAY HIDDEN
	DATA aCount           AS ARRAY HIDDEN
	
	DATA cDelimited       AS CHARACTER
	DATA lNumericNull     AS LOGICAL
	DATA lDateNull        AS LOGICAL
	DATA lStringNull      AS LOGICAL
	DATA cDecimalPoint    AS CHARACTER
	DATA cDateFormat      AS CHARACTER
	            
	METHOD Init()
	METHOD New()
	METHOD Execute()
	METHOD AddOnValid()
	METHOD AddOnCount()
	METHOD AddOnStruct()
	METHOD AddOnInstruction()
	
ENDCLASS
//--------------------------------------------------------------------------------
METHOD New(cLayOut) CLASS TLayOutTAX

PARAMTYPE 0  VAR cLayOut      AS CHARACTER

::Init()
::cLayOut      := cLayOut

Return Self
//--------------------------------------------------------------------------------
METHOD Init()  CLASS TLayOutTAX

DEFAULT ::aMsgStatus    := {}
DEFAULT ::aTables       := {}
DEFAULT ::aInit         := {}
DEFAULT ::aClose        := {}
DEFAULT ::aFormul       := {}

DEFAULT ::cDescricao    := ""
DEFAULT ::cDestino      := ""
DEFAULT ::cObjetivo     := ""
DEFAULT ::cPrazo        := ""
DEFAULT ::cAplicativo   := ""
DEFAULT ::cComentario   := ""
DEFAULT ::cClassName    := "TLAYOUTTAX"
DEFAULT ::oLayOutTax    := Self
DEFAULT ::cLayOut       := ""
DEFAULT ::cSaveFile     := ""
DEFAULT ::cMsgOutPutFile:= ""
DEFAULT ::cTempFile     := ""

Return
//--------------------------------------------------------------------------------
METHOD New(oParent,cID,cDescricao) CLASS TLayOutTAXTable
                   
PARAMTYPE 0  VAR oParent      AS OBJECT CLASS TLayOutTAX,TLayOutTAXTable
DEFAULT cID        := ""
DEFAULT cDescricao := ""

::Init()
         
::cID           := cID
::cDescription  := cDescricao

oParent:AddTables(Self)

Return Self
//--------------------------------------------------------------------------------
METHOD Init()  CLASS TLayOutTAXTable

DEFAULT ::cID         := ""
DEFAULT ::cDescription:= ""
DEFAULT ::cAlias      := ""
DEFAULT ::cClassName  := "TLAYOUTTAXTABLE"
DEFAULT ::aTables   := {}
DEFAULT ::aInit     := {}
DEFAULT ::aClose    := {}
DEFAULT ::aWhile    := {}
DEFAULT ::aEnd      := {}
DEFAULT ::aRecord   := {}

Return
//--------------------------------------------------------------------------------
METHOD New(oParent) CLASS TLayOutTAXRecord
                   
PARAMTYPE 0  VAR oParent      AS OBJECT CLASS TLayOutTAXTable

::Init()
         
oParent:AddRecord(Self)

Return Self
//--------------------------------------------------------------------------------
METHOD Init()  CLASS TLayOutTAXRecord

DEFAULT ::aTables       := {}
DEFAULT ::aValid        := {}
DEFAULT ::aEstrutura    := {}
DEFAULT ::aConteudo     := {}
DEFAULT ::aCount        := {}

DEFAULT ::cDelimited    := ""
DEFAULT ::cDateFormat   := "YYYYMMDD"
DEFAULT ::cClassName    := "TLAYOUTTAXRECORD"

DEFAULT ::lNumericNull  := .F.
DEFAULT ::lDateNull     := .F.
DEFAULT ::lStringNull   := .F.
DEFAULT ::cDecimalPoint := ""

Return
//--------------------------------------------------------------------------------
METHOD AddTables(oTable) CLASS TLayOutTAX

PARAMTYPE 0 VAR oTable AS OBJECT CLASS TLayOutTAXTable

Aadd(::aTables,oTable)

oTable:oParent    := Self
oTable:oLayOutTax := Self:oLayOutTax
Return
//--------------------------------------------------------------------------------
METHOD AddRecord(oRecord) CLASS TLayOutTAXTable

PARAMTYPE 0 VAR oRecord AS OBJECT CLASS TLayOutTAXRecord

Aadd(::aRecord,oRecord)

oRecord:oParent    := Self
oRecord:oLayOutTax := Self:oLayOutTax
Return
//--------------------------------------------------------------------------------
METHOD AddOnInit(cInstruction) CLASS TLayOutTAX

aadd(::aInit,cInstruction)
Return
//--------------------------------------------------------------------------------
METHOD AddOnClose(cInstruction) CLASS TLayOutTAX

aadd(::aClose,cInstruction)
Return
//--------------------------------------------------------------------------------
METHOD AddFormul(cInstruction) CLASS TLayOutTAX

aadd(::aFormul,cInstruction)
Return
//--------------------------------------------------------------------------------
METHOD AddOnInit(cInstruction) CLASS TLayOutTAXTable

aadd(::aInit,cInstruction)
Return
//--------------------------------------------------------------------------------
METHOD AddOnClose(cInstruction) CLASS TLayOutTAXTable

aadd(::aClose,cInstruction)
Return
//--------------------------------------------------------------------------------
METHOD AddOnWhile(cInstruction) CLASS TLayOutTAXTable

aadd(::aWhile,cInstruction)
Return
//--------------------------------------------------------------------------------
METHOD AddOnEnd(cInstruction) CLASS TLayOutTAXTable

aadd(::aEnd,cInstruction)
Return
//--------------------------------------------------------------------------------
METHOD AddOnValid(cInstruction) CLASS TLayOutTAXRecord

aadd(::aValid,cInstruction)
Return
//--------------------------------------------------------------------------------
METHOD AddOnCount(cInstruction) CLASS TLayOutTAXRecord

aadd(::aCount,cInstruction)
Return
//--------------------------------------------------------------------------------
METHOD AddOnStruct(aStruct) CLASS TLayOutTAXRecord

aadd(::aEstrutura,aStruct)
Return
//--------------------------------------------------------------------------------
METHOD AddOnInstruction(cInstrucao) CLASS TLayOutTAXRecord

aadd(::aConteudo,cInstrucao)
Return
//--------------------------------------------------------------------------------
METHOD ClassName() CLASS TLayOutTAX
Return ::cClassName
//--------------------------------------------------------------------------------
METHOD FindID(oObjeto,cID) CLASS TLayOutTAX
Local oFind
Local nX := 1

PARAMTYPE 0  VAR oObjeto      AS OBJECT CLASS TLayOutTAX,TLayOutTAXTable

If oObjeto:ClassName() == "TLAYOUTTAXTABLE" .And. oObjeto:cID == cID
	oFind := oObjeto
Else
	For nX := 1 To Len(oObjeto:aTables)
		oFind := oObjeto:FindID(oObjeto:aTables[nX],cID)
		If oFind <> Nil
			Exit
		EndIf	
	Next nX
EndIf
Return oFind
//--------------------------------------------------------------------------------
METHOD ReadLayOut(cVersion,cErro,cAviso) CLASS TLayOutTAX

Local aAux     := {}
Local aAux2    := {}
Local cLinha   := "" 
Local cAviso   := ""
Local nX       := 0
Local nY       := 0
Local lXml     := .T.
Local oTable
Local oRecord
Local oSuperior
Local bError

PRIVATE oXml
DEFAULT cErro := ""
DEFAULT cAviso:= ""

oXML := XmlParserFile(::cLayOut,"_",@cErro,@cAviso)
If ( Empty(cErro) .And. Empty(cAviso) .And. oXml <> Nil)

	// -----> Verifica se e um xml de layout
	bError := ErrorBlock({|| lXml := .F.})
	Begin Sequence	
		::cDescricao   := oXml:_LayOutTax:_description:_name:TEXT
		::cDestino     := oXml:_LayOutTax:_description:_destination:TEXT
		::cObjetivo    := oXml:_LayOutTax:_description:_object:TEXT
		::cPrazo       := oXml:_LayOutTax:_description:_delivery:TEXT
		::cAplicativo  := oXml:_LayOutTax:_description:_LayOutSystem:TEXT
		::cComentario  := oXml:_LayOutTax:_description:_comment:TEXT
		
		If ValType(oXml:_LayOutTax:_layout)=="A"
			aAux := oXml:_LayOutTax:_layout
		Else
			aAux := { oXml:_LayOutTax:_layout }
		EndIf		
		
		For nX := 1 To Len(aAux)
			If !Empty(cVersion) .And. AllTrim(aAux[nX]:_version:TEXT) == AllTrim(cVersion)
				oXml := aAux[nX]
				Exit
			ElseIf nX == Len(aAux)
				oXml := aAux[nX]
			EndIf
		Next nX
	End Sequence	
	ErrorBlock(bError)

	If lXml 		
		// -----> Verifica os bloco de inicializacao e encerramento do layout
		If Type("oXml:_initiating:_instruction")<>"U"
			If ValType(oXml:_initiating:_instruction)=="A"
				aAux2 := oXml:_initiating:_instruction
			Else
				aAux2 := { oXml:_initiating:_instruction }
			EndIf		  				  		  		
	  		For nY := 1 To Len(aAux2)
	  			::AddOnInit(aAux2[nY]:TEXT)
	  	    Next nY
	  	EndIf
		If Type("oXml:_finishing:_instruction")<>"U"
			If ValType(oXml:_finishing:_instruction)=="A"
				aAux2 := oXml:_finishing:_instruction
			Else
				aAux2 := { oXml:_finishing:_instruction }
			EndIf		  				  		  		
	  		For nY := 1 To Len(aAux2)
	  			::AddOnClose(aAux2[nY]:TEXT)
	  	    Next nY
	  	EndIf
		// -----> Pesquisa os registros do layout
		If ValType(oXml:_Registers:_Register)=="A"
			aAux := oXml:_Registers:_Register
		Else
			aAux := { oXml:_Registers:_Register }
		EndIf
		For nX := 1 To Len(aAux)
	  		oXml:= aAux[nX]
			// -----> Pesquisa o registro pai do registro, se houver
	  		If Type("oXml:_superior")<>"U"
	  			oSuperior := ::FindID(Self,oXml:_superior:TEXT)
	  		Else
		  		oSuperior := Self
		  	EndIf
			// -----> Inicializa o registro na classe
	  		oTable := TLayOutTAXTable():New(oSuperior,oXml:_id:TEXT,oXml:_description:TEXT)
			// -----> Verifica os bloco de inicializacao e encerramento do registro
	  		If Type("oXml:_initiating:_instruction")<>"U"
	  			If ValType(oXml:_initiating:_instruction)=="A"
					aAux2 := oXml:_initiating:_instruction
				Else
					aAux2 := { oXml:_initiating:_instruction }
				EndIf		  				  		  		
		  		For nY := 1 To Len(aAux2)
		  			oTable:AddOnInit(aAux2[nY]:TEXT)
	  		    Next nY
	  		EndIf
	  		If Type("oXml:_finishing:_instruction")<>"U"
	  			If ValType(oXml:_finishing:_instruction)=="A"
					aAux2 := oXml:_finishing:_instruction
				Else
					aAux2 := { oXml:_finishing:_instruction }
				EndIf		  				  		  		
		  		For nY := 1 To Len(aAux2)
		  			oTable:AddOnClose(aAux2[nY]:TEXT)
	  		    Next nY
	  		EndIf
			// -----> Verifica a tabela do registro se houver
	  		If Type("oXml:_Table")<>"U"
	  			oTable:cAlias := oXml:_Table:TEXT
	  		EndIf
			// -----> Verifica os campos do registro
	  		oRecord := TLayOutTAXRecord():New(oTable) 
			// -----> Verifica as propriedades dos campos
	  		If Type("oXml:_lines:_Delimited")<>"U"
	  			oRecord:cDelimited := oXml:_lines:_Delimited:TEXT
	  		EndIf
	  		If Type("oXml:_lines:_NumericNull")<>"U"
	  			oRecord:lNumericNull := IIF(oXml:_lines:_NumericNull:TEXT=="T",.T.,.F.)
	  		EndIf
	  		If Type("oXml:_lines:_DecimalPoint")<>"U"
	  			oRecord:cDecimalPoint := oXml:_lines:_DecimalPoint:TEXT
	  		EndIf
	  		If Type("oXml:_lines:_DateFormat")<>"U"
	  			oRecord:cDateFormat := oXml:_lines:_DateFormat:TEXT
	  		EndIf
	  		If Type("oXml:_lines:_DateNull")<>"U"
	  			oRecord:lDateNull := IIF(oXml:_lines:_DateNull:TEXT=="T",.T.,.F.)
	  		EndIf
	  		If Type("oXml:_lines:_StringNull")<>"U"
	  			oRecord:lStringNull := IIF(oXml:_lines:_StringNull:TEXT=="T",.T.,.F.)
	  		EndIf	  		
			// -----> Verifica os bloco de inicializacao e encerramento dos campos
	  		If Type("oXml:_lines:_initiating:_instruction")<>"U"
	  			If ValType(oXml:_lines:_initiating:_instruction)=="A"
					aAux2 := oXml:_lines:_initiating:_instruction
				Else
					aAux2 := { oXml:_lines:_initiating:_instruction }
				EndIf		  				  		  		
		  		For nY := 1 To Len(aAux2)
		  			oTable:AddOnWhile(aAux2[nY]:TEXT)
	  		    Next nY
	  		EndIf
	  		If Type("oXml:_lines:_finishing:_instruction")<>"U"
	  			If ValType(oXml:_lines:_finishing:_instruction)=="A"
					aAux2 := oXml:_lines:_finishing:_instruction
				Else
					aAux2 := { oXml:_lines:_finishing:_instruction }
				EndIf		  				  		  		
		  		For nY := 1 To Len(aAux2)
		  			oTable:AddOnEnd(aAux2[nY]:TEXT)
	  		    Next nY
	  		EndIf
	  		If Type("oXml:_lines:_validline:_instruction")<>"U"
	  			If ValType(oXml:_lines:_validline:_instruction)=="A"
					aAux2 := oXml:_lines:_validline:_instruction
				Else
					aAux2 := { oXml:_lines:_validline:_instruction }
				EndIf
		  		For nY := 1 To Len(aAux2)
		  			oRecord:AddOnValid(aAux2[nY]:TEXT)
	  		    Next nY
	  		EndIf 
	  		If Type("oXml:_lines:_count:_instruction")<>"U"
	  			If ValType(oXml:_lines:_count:_instruction)=="A"
					aAux2 := oXml:_lines:_count:_instruction
				Else
					aAux2 := { oXml:_lines:_count:_instruction }
				EndIf		  				  		  		
		  		For nY := 1 To Len(aAux2)
		  			oRecord:AddOnCount(aAux2[nY]:TEXT)
	  		    Next nY
	  		EndIf 
			// -----> Atualiza a classe com as definicoes dos campos
			If ValType(oXml:_lines:_fields:_field)=="A"
				aAux2 := oXml:_lines:_fields:_field
			Else
				aAux2 := { oXml:_lines:_fields:_field }
			EndIf				
			For nY := 1 To Len(aAux2)
				oXml := aAux2[nY]
				
				oRecord:AddOnStruct({oXml:_name:TEXT,oXml:_type:TEXT,Val(oXml:_size:TEXT),Val(oXml:_dec:TEXT)})
				oRecord:AddOnInstrution(oXml:_instruction:TEXT)
			
			Next nY		  		
	    Next nX
	  EndIf
Else
	lXml := .F.
EndIf

Return(lXml)

//--------------------------------------------------------------------------------
METHOD Execute(bBlock) CLASS TLayOutTAX

Local nX         := 0
Local nY         := 0
Local lExecutado := .T.
Local cLinha     := ""
Local cVar       := ""
Local bError

Local cArqSPED 	 := "SPEDFILE.TXT"

Local lCompress  := .T. //GetNewPar( "MV_SPDCOMP" , .T. 				) // parametro de compressão do arquivo 
Local cSpedFDir	 := "\web\ftpsped\" // GetNewPar( "MV_SPDFDIR" , "\web\ftpsped\"  	) // parametro de utilização para setar o diretorio do FTP 

DEFAULT bBlock := {|| .T.}
            
// -----> Cria arquivo temporario      
::cTempFile := CriaTrab(,.F.)
::nHandle   := FCreate(::cTempFile+".TMP",0)

If ( FError() == 0 )
	
	aEval(::aInit,{|x| &(x) })
	
	For nX := 1 To Len(::aTables)
		Eval(bBlock,::aTables[nX]:cID,::aTables[nX]:cDescription)
		::aTables[nX]:Execute()	
	Next nX
	
	aEval(::aClose,{|x| &(x) })
	
Else
	lExecutado := .F.
EndIf

// -----> Fecha arquivo temporario
FClose(::nHandle)
::nHandle   := FCreate(::cTempFile+".TXT",0)

// -----> Executa a formulas
FT_FUse(::cTempFile+".TMP")
FT_FGotop()
While !FT_FEof()
	cLinha := FT_FReadLn()
	If "@@@" $ cLinha
		For nX := 1 To Len(::aFormul)
			bError     := ErrorBlock({|e| Aviso("Erro","Erro de formula -> "+::aFormul[nX],{"Ok"}) })
			Begin Sequence
				cVar := &(::aFormul[nX])
			End Sequence
			ErrorBlock(bError)
			cLinha := StrTran(cLinha,"@@@"+::aFormul[nX]+"@@@",cVar)
		Next nX
	EndIf
	FWrite(::nHandle,cLinha+CRLF)
	FT_FSkip()
EndDo

// -----> Encerra arquivo magnetico
FT_FUse()
FClose(::nHandle)
FErase(::cTempFile+".TMP")
                                        
// gero o arquivo no SYSTEM do TSS
If !Empty(cArqSPED)

	Ferase(cArqSPED)
	FRename(::cTempFile+".TXT",cArqSPED)

	If File(cArqSPED)

		MakeDir( cSpedFDir )				// Caso nao exista o diretorio informado, cria-lo
		FErase ( cSpedFDir + cArqSPED )	// Se o arquivo informado existir no diretorio destino, apaga-lo

		If lCompress // verifica se o parametro de compactação está ativo.

			If ! Empty(::cMsgOutPutFile)
				MemoWrite(::cMsgOutPutFile,"Efetuando a compactação do arquivo e copiando para o FTP" + CRLF + CRLF)
			EndIf

			// efetuo a limpeza do arquivo do diretorio
			Ferase( cSpedFDir + "ArqSPED.MZP" )
			
			// efetuo a compressão do arquivo e gravo no diretorio do FTP
		    MsCompress( cArqSPED , cSpedFDir + "ArqSPED.MZP" )

	    Else // somente efetua a copia do arquivo para o FTP
			
			If ! Empty(::cMsgOutPutFile)
				MemoWrite(::cMsgOutPutFile,"Efetuando a copia do arquivo para o FTP" + CRLF + CRLF)
			EndIf
		
			// efetuo a limpeza do arquivo do diretorio
			Ferase( cSpedFDir + cArqSPED )
			
			// copio o arquivo para o diretorio do FTP.
	    	__CopyFile( cArqSPED, cSpedFDir )
	    Endif

		// efetuo a limpeza do arquivo
		FErase(::cTempFile+".TXT")
	EndIf
EndIf

FErase(::cMsgOutPutFile)

Return lExecutado

//--------------------------------------------------------------------------------
METHOD Execute() CLASS TLayOutTAXTable

Local aArea      := GetArea()
Local nX         := 0
Local lValido    := .T.
Local bWhile     :={|| lValido }
Local cMsg       := ""

nX := aScan(::oLayOutTax:aMsgStatus,{|x| x[1] == ::cID})
If nX == 0
	aadd(::oLayOutTax:aMsgStatus,{::cID,PadR(::cDescription,40),1})
	nX := Len(::oLayOutTax:aMsgStatus)
Else
	::oLayOutTax:aMsgStatus[nX][3]++
EndIf

If !Empty(::oLayOutTax:cMsgOutPutFile)
	cMsg += "Registro: "+::oLayOutTax:aMsgStatus[nX][1]+" - "+::oLayOutTax:aMsgStatus[nX][2]+": "+Str(::oLayOutTax:aMsgStatus[nX][3],8)+CRLF
	cMsg += CRLF
	For nX := 1 To Len(::oLayOutTax:aMsgStatus)
		cMsg += "Registro: "+::oLayOutTax:aMsgStatus[nX][1]+" - "+::oLayOutTax:aMsgStatus[nX][2]+": "+Str(::oLayOutTax:aMsgStatus[nX][3],8)+CRLF
	Next nX

	MemoWrite(::oLayOutTax:cMsgOutPutFile,cMsg)
EndIf

aEval(::aInit,{|x| &(x) })

If !Empty(::cAlias)
	dbSelectArea(::cAlias)
	bWhile := {|| !Eof() .And. lValido }
EndIf

While Eval(bWhile)
	If !Empty(::cAlias)
		dbSelectArea(::cAlias)
	EndIf	
	aEval(::aWhile,{|x| lValido := IIf(lValido,&(x),.F.) })
	If lValido
		For nX := 1 To Len(::aRecord)
			::aRecord[nX]:Execute()
			For nX := 1 To Len(::aTables)			
				::aTables[nX]:Execute()
			Next nX
		Next nX	
		If !Empty(::cAlias)
			dbSelectArea(::cAlias)
		EndIf
	EndIf	
	aEval(::aEnd,{|x| lValido := IIf(lValido,&(x),.F.) })
	If !Empty(::cAlias) .And. lValido
		dbSelectArea(::cAlias)
		dbSkip()
	Else
		lValido := .F.
	EndIf
EndDo

aEval(::aClose,{|x| &(x) })

RestArea(aArea)
Return 

//--------------------------------------------------------------------------------
METHOD Execute() CLASS TLayOutTAXRecord

Local aArea      := GetArea()
Local lValido    := .T.
Local nX         := 0
Local cRecord    := ""
Local bError
Local uConteudo

aEval(::aValid,{|x| lValido := IIf(lValido,&(x),.F.) })

If lValido
	cRecord += ::cDelimited
	For nX := 1 To Len(::aEstrutura)
		bError     := ErrorBlock({|e| Aviso("Erro","Erro de formula -> "+::oParent:cID+":"+::oParent:cDescription+"/"+::aEstrutura[nX][1],{"Ok"}) })
		If ::aEstrutura[nX][2] <> "F"
			Begin Sequence           
				uConteudo := &(::aConteudo[nX])
			End Sequence
		Else
			uConteudo := ::aConteudo[nX]
		EndIf
		ErrorBlock(bError)
		Do Case
			Case ::aEstrutura[nX][2] == "C"
				If ::aEstrutura[nX][3] == 0 .Or. (Empty(uConteudo) .And. Self:lStringNull)
					cRecord += AllTrim(uConteudo)
				Else
					cRecord += PadR(uConteudo,Self:aEstrutura[nX][3])
				EndIf
			Case ::aEstrutura[nX][2] == "F"
				cRecord += "@@@"+AllTrim(uConteudo)+"@@@"
				::oLayOutTax:AddFormul(AllTrim(uConteudo))
			Case Self:aEstrutura[nX][2] == "N"
				If ValType(uConteudo) == "C"
					uConteudo := Val(uConteudo)
				EndIf
				If Self:aEstrutura[nX][3]==0
					Self:aEstrutura[nX][3] := 18
				EndIf
				Do Case
					Case Self:lNumericNull .And. uConteudo == 0 .And. !Empty(Self:cDelimited)
						cRecord += ""
					Case Self:lNumericNull .And. uConteudo == 0 .And. Empty(Self:cDelimited)
						cRecord += Space(Self:aEstrutura[nX][3])
					Case !Empty(Self:cDecimalPoint) .And. !Empty(Self:cDelimited)
						cRecord += AllTrim(StrTran(Str(uConteudo,Self:aEstrutura[nX][3],Self:aEstrutura[nX][4]),".",Self:cDecimalPoint))
					Case !Empty(Self:cDecimalPoint) .And. Empty(Self:cDelimited)
						cRecord += StrTran(StrZero(uConteudo,Self:aEstrutura[nX][3],Self:aEstrutura[nX][4]),".",Self:cDecimalPoint)
					Case Empty(Self:cDecimalPoint) .And. !Empty(Self:cDelimited)
						uConteudo := NoRound(uConteudo*(10**(Self:aEstrutura[nX][4])),Self:aEstrutura[nX][4])
						cRecord += AllTrim(Str(uConteudo,Self:aEstrutura[nX][3]))
					Case Empty(Self:lDecimalPoint) .And. Empty(Self:cDelimited)
						uConteudo := NoRound(uConteudo*(10**(Self:aEstrutura[nX][4])),Self:aEstrutura[nX][4])
						cRecord += StrZero(uConteudo,Self:aEstrutura[nX][3])
				EndCase
			Case Self:aEstrutura[nX][2] == "D"
				Do Case
					Case Self:lDateNull .And. Empty(uConteudo)
						cRecord += ""
					Case Empty(Self:cDateFormat)
						cRecord += SpedDateConv(uConteudo)
					OtherWise
						cRecord += SpedDateConv(uConteudo,Self:cDateFormat)
				EndCase
		EndCase
		cRecord += ::cDelimited
	Next nX
	aEval(::aCount,{|x| &(x) })
EndIf
If !Empty(cRecord)
	FWrite(::oLayOutTax:nHandle,cRecord+CRLF)
EndIF
RestArea(aArea)
Return(lValido)

user function tstsped()

Local oteste := TLayOutTAX():New("spedctb.xml")
Local cVar := "" 
PRIVATE lMsHelpAuto:=.t.
PRIVATE oParam

InitSped()

cVar := "<Param>"
cVar += "<DT_INI>20060101</DT_INI>"
cVar += "<DT_FIM>20060630</DT_FIM>"
cVar += "<ORDEM>1</ORDEM>"
cVar += "</Param>"

oTeste:ReadLayOut()

oParam := XmlParser(cVar,"_","","")
oParam := oParam:_Param

oTeste:Execute()

Return(.T.)